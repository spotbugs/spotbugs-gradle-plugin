plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'signing'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'com.diffplug.gradle.spotless' version '4.0.0'
    id 'net.ltgt.errorprone' version '1.1.1'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.sonarqube' version '2.8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'com.github.spotbugs.snom'

repositories {
    // To download the Android Gradle Plugin
    google()
    // To download trove4j required by the Android Gradle Plugin
    jcenter()
}

ext {
    errorproneVersion = '2.3.4'
    spotBugsVersion = '4.0.6'
    slf4jVersion = '1.8.0-beta4'
    androidGradlePluginVersion = '4.0.0'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    compileOnly localGroovy()
    compileOnly "com.github.spotbugs:spotbugs:${spotBugsVersion}"
    compileOnly "com.android.tools.build:gradle:${androidGradlePluginVersion}"
    testImplementation 'com.tngtech.archunit:archunit:0.13.1'
}

groovydoc {
    docTitle 'SpotBugs Gradle Plugin'
    link 'https://docs.gradle.org/current/javadoc/', 'org.gradle.api.'
    link 'https://docs.oracle.com/en/java/javase/11/docs/api/', 'java.'
    link 'https://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.'
}

def signingKey = System.getenv("SIGNING_KEY")
def signingPassword = System.getenv("SIGNING_PASSWORD")

signing {
    if(signingKey != null &&
    signingPassword != null &&
    !signingKey.isEmpty() &&
    !signingPassword.isEmpty()){
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign configurations.archives
    }
    else{
        logger.warn('The signing key and password are null. This can be ignored if this is a pull request.')
    }
}



task processVersionFile(type: WriteProperties) {
    outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

    property 'slf4j-version', slf4jVersion
    property 'spotbugs-version', spotBugsVersion
}
tasks.processResources.dependsOn processVersionFile

apply from: "$rootDir/gradle/test.gradle"
apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/publish.gradle"

defaultTasks 'spotlessApply', 'build'