plugins{
  id 'groovy'
  id 'java-gradle-plugin'
  id "com.gradle.plugin-publish" version "0.9.7"
  id 'org.sonarqube' version '2.6.2'
}

apply from: "$rootDir/gradle/checkstyle.gradle"
apply from: "$rootDir/gradle/jacoco.gradle"
apply from: "$rootDir/gradle/java.gradle"
apply from: "$rootDir/gradle/sonar.gradle"
apply from: "$rootDir/gradle/deploy.gradle"

version = '2.0.0-SNAPSHOT'
group = "com.github.spotbugs"
def spotBugsVersion = '3.1.9'
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
  jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compileOnly "com.github.spotbugs:spotbugs:${spotBugsVersion}"

    testCompile gradleTestKit()
    testCompile 'junit:junit:4.12'//, 'org.spockframework:spock-core:1.0-groovy-2.4'
}

test {
  // show test output
  testLogging.showStandardStreams = true
}

// The configuration example below shows the minimum required properties
// configured to publish your plugin to the plugin portal
pluginBundle {
  website = 'https://spotbugs.github.io/'
  vcsUrl = 'https://github.com/spotbugs/spotbugs-gradle-plugin'
  description = "Performs quality checks on your project's Java source files using SpotBugs and generates reports from these checks"
  tags = ['spotbugs', 'findbugs', 'code quality']

  plugins {
    spotbugsPlugin {
      id = 'com.github.spotbugs'
      displayName = 'SpotBugs Gradle plugin'
    }
  }
}

task processVersionFile(type: WriteProperties) {
  outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

  property 'spotbugs-version', spotBugsVersion
}
tasks.processResources.dependsOn processVersionFile

tasks.publishPlugins.doFirst {
  if (version.endsWith('-SNAPSHOT')) {
    throw new StopExecutionException("Skip publishing Gradle plugin because its version is not stable: ${version}")
  }
  if (spotBugsVersion.endsWith('-SNAPSHOT')) {
    throw new StopExecutionException("Skip publishing Gradle plugin because SpotBugs version is not stable: ${spotBugsVersion}")
  }
}
