plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'jacoco'
    id 'signing'
    id 'com.gradle.plugin-publish' version '0.15.0'
    id 'com.diffplug.spotless' version '5.12.5'
    id 'net.ltgt.errorprone' version '2.0.2'
    id 'org.sonarqube' version '3.2.0'
    id 'com.github.spotbugs' version '4.7.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
group = 'com.github.spotbugs.snom'

repositories {
    // To download the Android Gradle Plugin
    google()
    // To download trove4j required by the Android Gradle Plugin
    mavenCentral()
}

ext {
    errorproneVersion = '2.7.1'
    spotBugsVersion = '4.2.3'
    slf4jVersion = '1.8.0-beta4'
    androidGradlePluginVersion = '4.2.1'
}

dependencies {
    errorprone "com.google.errorprone:error_prone_core:${errorproneVersion}"
    compileOnly localGroovy()
    compileOnly "com.github.spotbugs:spotbugs:${spotBugsVersion}"
    compileOnly "com.android.tools.build:gradle:${androidGradlePluginVersion}"
    testImplementation 'com.tngtech.archunit:archunit:0.18.0'
}

tasks.named('groovydoc') {
    docTitle 'SpotBugs Gradle Plugin'
    link 'https://docs.gradle.org/current/javadoc/', 'org.gradle.api.'
    link 'https://docs.oracle.com/en/java/javase/11/docs/api/', 'java.'
    link 'https://docs.groovy-lang.org/latest/html/gapi/', 'groovy.', 'org.codehaus.groovy.'
}

def signingKey = System.getenv("SIGNING_KEY")
def signingPassword = System.getenv("SIGNING_PASSWORD")

signing {
    if(signingKey != null &&
    signingPassword != null &&
    !signingKey.isEmpty() &&
    !signingPassword.isEmpty()){
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign configurations.archives
    }
    else{
        logger.warn('The signing key and password are null. This can be ignored if this is a pull request.')
    }
}

spotbugs {
    ignoreFailures = true
}
spotbugsMain {
    reports {
        sarif {
            enabled = true
        }
    }
}

def processVersionFile = tasks.register('processVersionFile', WriteProperties) {
    outputFile file('src/main/resources/spotbugs-gradle-plugin.properties')

    property 'slf4j-version', slf4jVersion
    property 'spotbugs-version', spotBugsVersion
}
tasks.named('processResources').configure {
    dependsOn processVersionFile
}

apply from: "$rootDir/gradle/test.gradle"
apply from: "$rootDir/gradle/functional-test.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/errorprone.gradle"
apply from: "$rootDir/gradle/publish.gradle"

defaultTasks 'spotlessApply', 'build'
