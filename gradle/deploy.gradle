// see https://central.sonatype.org/pages/gradle.html
// and https://docs.gradle.org/current/userguide/publishing_maven.html

apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
  repositories {
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots/"
      credentials {
        username = project.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
        password = project.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
      }
    }
  }
  publications {
    pluginMaven(MavenPublication) { // add config to the publications generated by java-gradle-plugin. https://github.com/gradle/gradle/issues/10384#issuecomment-530347578
      artifact sourcesJar
      artifact javadocJar
      pom {
        description = 'A Gradle plugin for SpotBugs'
        url = 'https://github.com/spotbugs/spotbugs-gradle-plugin/'
        scm {
          connection = 'scm:git:git@github.com:spotbugs/spotbugs-gradle-plugin.git'
          developerConnection = 'scm:git:git@github.com:spotbugs/spotbugs-gradle-plugin.git'
          url = 'https://github.com/spotbugs/spotbugs-gradle-plugin/'
        }
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
      }
    }
  }
}
