configurations {
    functionalTestImplementation {
        extendsFrom testImplementation
    }
}
dependencies {
    functionalTestImplementation 'org.spockframework:spock-core:2.0-M5-groovy-3.0'
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functionalTest/groovy')
        resources.srcDir file('src/functionalTest/resources')
        compileClasspath += sourceSets.main.output
        runtimeClasspath += output + compileClasspath
    }
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

def functionalTest = tasks.register('functionalTest', Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    mustRunAfter test
    systemProperty 'snom.test.functional.gradle', System.getProperty('snom.test.functional.gradle', gradle.gradleVersion)
}

tasks.named('check').configure {
    dependsOn functionalTest
}
