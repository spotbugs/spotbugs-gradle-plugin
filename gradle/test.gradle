ext {
    junitVersion = '5.6.0'
}
dependencies {
    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.named('test', Test) {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()
}

jacoco {
    toolVersion = "0.8.5"
}
def jacocoTestReport = tasks.named('jacocoTestReport') {
    reports {
        xml {
            enabled true
        }
    }
}
tasks.named('check').configure {
    dependsOn jacocoTestReport
}

sonarqube {
    properties {
        property "sonar.projectKey", "com.github.spotbugs.gradle"
        property "sonar.organization", "spotbugs"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.reports.xml.destination
    }
}
tasks.named('sonarqube').configure {
    mustRunAfter jacocoTestReport
}
