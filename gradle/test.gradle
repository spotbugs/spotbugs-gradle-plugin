ext {
    junitVersion = '5.7.2'
}
dependencies {
    testImplementation gradleTestKit()
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
    maxParallelForks = Runtime.runtime.availableProcessors()
}

def jacocoTestReport = tasks.named('jacocoTestReport') {
    reports {
        xml {
            required = true
        }
    }
}
tasks.named('check').configure {
    dependsOn jacocoTestReport
}

sonarqube {
    properties {
        property "sonar.projectKey", "com.github.spotbugs.gradle"
        property "sonar.organization", "spotbugs"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.jacoco.xmlReportPaths", jacocoTestReport.map { it.reports.xml.destination }
    }
}
tasks.named('sonarqube').configure {
    mustRunAfter jacocoTestReport
}
